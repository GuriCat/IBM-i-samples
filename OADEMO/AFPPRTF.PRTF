      * CRTPRTF FILE(AFPPRTF) SRCFILE(EXPRINT)
      *         DEVTYPE(*AFPDS) PAGESIZE(11 8 *UOM) UOM(*INCH)
      * CPYSPLF FILE(AFPPRTF) TOFILE(*TOSTMF) SPLNBR(*LAST)
      *         TOSTMF('/tmp/afpprtf.pdf') WSCST(*PDF)
      *
     A          R IMG                       AFPRSC(&IMGF *JFIF &IY &IX +
     A                                      (*PATH &IMGP) +
     A                                      (*MAPOPT *ST) +
     A                                      (*SIZE &IMGH &IMGW))
     A            IMGF         128   P
     A            IMGH           5S 3P
     A            IMGW           5S 3P
     A            IY             5S 3P
     A            IX             5S 3P
     A            IMGP         640   P
      *
     A          R BAR1
     A            EANDAT         7S 0       BARCODE(EAN8 2 *HRI +
     A                                      (*WIDTH .012))
     A                                      POSITION(&B1Y &B1X)
     A            B1Y            5S 3P
     A            B1X            5S 3P
      *
     A          R BAR2
      *                   高さ1インチ
      *                   誤り訂正レベルM／バイナリ文字数106→バージョン6
      *                    *CONVERT(4)→5026から943に変換
     A            QRDATA       106O         BARCODE(QRCODE 1 *HRZ +
     A                                      X'02' (*WIDTH .02) (*SWIDTH 0.8) +
     A                                      (*QRCODE 6 1 *CONVERT(4) *TRIM))
     A
     A                                      POSITION(&B2Y &B2X)
     A            B2Y            5S 3P
     A            B2X            5S 3P
      *
     A          R STR01
     A
     A            UNISTR       100G         FONTNAME(&FON +
     A                                      (*POINTSIZE &FOH &FOW))
     A                                      CCSID(1200 *NOCONVERT)
     A                                      POSITION(&SY &SX)
     A            FON           30A  P
     A            FOH            4S 1P
     A            FOW            4S 1P
     A            SY             5S 3P
     A            SX             5S 3P
      *
     A          R BOX
     A                                      BOX(&BY1 &BX1 &BY2 &BX2 &BLW +
     A                                      (*COLOR *RGB 40 50 100))
     A            BY1            5S 3P
     A            BX1            5S 3P
     A            BY2            5S 3P
     A            BX2            5S 3P
     A            BLW            5S 3P
      *
     A          R HLINE
     A                                      LINE(&HLY &HLX &HLL *HRZ &HLW +
     A                                      (*COLOR *RGB 70 80 100))
     A            HLY            5S 3P
     A            HLX            5S 3P
     A            HLL            5S 3P
     A            HLW            5S 3P
      *
     A          R VLINE
     A                                      LINE(&VLY &VLX &VLL *VRT &VLW +
     A                                      (*COLOR *RGB 70 80 100))
     A            VLY            5S 3P
     A            VLX            5S 3P
     A            VLL            5S 3P
     A            VLW            5S 3P
      *
     A          R EOP                       ENDPAGE
      *
